# Test Syracuse Dom 2
# Un petit test sur diverses fonctions de tableaux: déclarations, passages d'arguments, tris, recherches
 
Global Integer OPTION_TRACE, ERREUR_TROUVEE
Global Char FICHIER_TRACE(200)
Local Integer TIME_START,NOMBRE
 
  [V]OPTION_TRACE=1
  [V]FICHIER_TRACE=filpath("TRA",trtcou,"tra")
 
TIME_START=time
 
 
NOMBRE=22
Local Char TAB_CHAINE(50)(NOMBRE)
Local Decimal TAB_DECIMAL(NOMBRE)
Local Date TAB_DATE(1..NOMBRE)
 
Local Integer I
 
TAB_CHAINE(I)="Prison de la Bastille"            : TAB_DECIMAL(I)= 3   : TAB_DATE(I+1)=[14/7/1789] : I+=1
TAB_CHAINE(I)="Armand Jean Du Plessis Richelieu" : TAB_DECIMAL(I)= 14  : TAB_DATE(I+1)=[4/12/1642] : I+=1
TAB_CHAINE(I)="Jean Sébastien Bach"              : TAB_DECIMAL(I)= 159 : TAB_DATE(I+1)=[28/7/1750] : I+=1
TAB_CHAINE(I)="Georges Pompidou"                 : TAB_DECIMAL(I)= 26  : TAB_DATE(I+1)=[2/4/1974]  : I+=1
TAB_CHAINE(I)="Richard Wagner"                   : TAB_DECIMAL(I)= 53  : TAB_DATE(I+1)=[13/2/1883] : I+=1
TAB_CHAINE(I)="Georges Perec"                    : TAB_DECIMAL(I)= 58  : TAB_DATE(I+1)=[3/3/1982]  : I+=1
TAB_CHAINE(I)="Mur de Berlin"                    : TAB_DECIMAL(I)= 97  : TAB_DATE(I+1)=[9/11/1989] : I+=1
TAB_CHAINE(I)="François Mitterrand"              : TAB_DECIMAL(I)= 93  : TAB_DATE(I+1)=[8/1/1996]  : I+=1
TAB_CHAINE(I)="Johannes Brahms"                  : TAB_DECIMAL(I)= 23  : TAB_DATE(I+1)=[3/4/1897]  : I+=1
TAB_CHAINE(I)="Emile Zola"                       : TAB_DECIMAL(I)= 84  : TAB_DATE(I+1)=[29/9/1902] : I+=1
TAB_CHAINE(I)="Louis XVI"                        : TAB_DECIMAL(I)= 62  : TAB_DATE(I+1)=[21/1/1793] : I+=1
TAB_CHAINE(I)="François Marie Arouet Voltaire"   : TAB_DECIMAL(I)= 64  : TAB_DATE(I+1)=[30/5/1778] : I+=1
TAB_CHAINE(I)="John Fitzgerald Kennedy"          : TAB_DECIMAL(I)= 33  : TAB_DATE(I+1)=[22/11/1963]: I+=1
TAB_CHAINE(I)="World Trade Center"               : TAB_DECIMAL(I)= 83  : TAB_DATE(I+1)=[11/9/2001] : I+=1
TAB_CHAINE(I)="Felix Faure"                      : TAB_DECIMAL(I)= 27  : TAB_DATE(I+1)=[16/2/1899] : I+=1
TAB_CHAINE(I)="Albert Einstein"                  : TAB_DECIMAL(I)= 950 : TAB_DATE(I+1)=[18/4/1955] : I+=1
TAB_CHAINE(I)="Blaise Pascal"                    : TAB_DECIMAL(I)= 288 : TAB_DATE(I+1)=[19/8/1622] : I+=1
TAB_CHAINE(I)="Dmitri Chostakovitch"             : TAB_DECIMAL(I)= 41  : TAB_DATE(I+1)=[9/8/1975]  : I+=1
TAB_CHAINE(I)="Coco Chanel"                      : TAB_DECIMAL(I)= 97  : TAB_DATE(I+1)=[10/1/1971] : I+=1
TAB_CHAINE(I)="Martin Luther King"               : TAB_DECIMAL(I)= 16  : TAB_DATE(I+1)=[4/4/1968]  : I+=1
TAB_CHAINE(I)="Leonhard Euler"                   : TAB_DECIMAL(I)= 93  : TAB_DATE(I+1)=[18/9/1783] : I+=1
TAB_CHAINE(I)="Ernst Ingmar Bergmann"            : TAB_DECIMAL(I)= 99  : TAB_DATE(I+1)=[30/7/2007] : I+=1
Call TRACE_ERRBOX("Les décimales"-num$(TAB_DECIMAL(uni(TAB_DECIMAL)-1))
&-"se répètent en position"-num$(find(TAB_DECIMAL(uni(TAB_DECIMAL)-1),TAB_DECIMAL)-1)
&-"et"-num$(uni(TAB_DECIMAL)-1)) From ZSYRADOM1
Call TRACE_ERRBOX("Nombre="+num$(I)) From ZSYRADOM1
Call TRACE_ERRBOX(num$(TAB_DECIMAL(0))+"."+sigma(1,NOMBRE-1,num$(TAB_DECIMAL(indcum)))) From ZSYRADOM1
Call TRACE_ERRBOX(num$(TAB_DECIMAL(0))+"."+num$(sum(TAB_DECIMAL(1..NOMBRE-1)))) From ZSYRADOM1
Call TRACE_ERRBOX("Les chiffres varient entre"-num$(min(TAB_DECIMAL))-"et"-num$(max(TAB_DECIMAL))) From ZSYRADOM1
Call TRACE_ERRBOX("Somme:"-num$(sum(TAB_DECIMAL))-"Produit:"-num$(prd(TAB_DECIMAL)/(10^(-1+int(log(max(prd(TAB_DECIMAL),10))))))) From ZSYRADOM1
Call TRACE_ERRBOX("Moyenne:"-num$(avg(TAB_DECIMAL))-"Somme/nombre:"-num$(sum(TAB_DECIMAL)/dim(TAB_DECIMAL))) From ZSYRADOM1
Call TRACE_ERRBOX("Les chaines varient entre"-min(TAB_CHAINE)-"et"-max(TAB_CHAINE)) From ZSYRADOM1
Call TRACE_ERRBOX("Les dates varient entre"-num$(min(TAB_DATE))-"et"-num$(max(TAB_DATE))) From ZSYRADOM1
Call TRACE_ERRBOX(num$(TAB_DECIMAL(0))+"."+num$(sum(TAB_DECIMAL(1..NOMBRE-1)))) From ZSYRADOM1
Call TRIE_MOI_CA(TAB_CHAINE, TAB_DECIMAL, TAB_DATE)
 
Onerrgo TROP_LOIN
Call TRACE_ERRBOX("Compte="+num$(find("",TAB_CHAINE)-2+(find("",TAB_CHAINE)=0)*(dim(TAB_CHAINE)+1))) From ZSYRADOM1
For I=0 To find("",TAB_CHAINE)-2+(find("",TAB_CHAINE)=0)*(dim(TAB_CHAINE)+1)
  Call TRACE_ERRBOX (vireblc(TAB_CHAINE(I),3)-"score"-num$(TAB_DECIMAL(I))) From ZSYRADOM1
Next I
 
Call RETRIE_MOI_CA(TAB_CHAINE, TAB_DECIMAL, TAB_DATE)
 
For I=0 To find("",TAB_CHAINE)-2+(find("",TAB_CHAINE)=0)*(dim(TAB_CHAINE)+1)
  Call TRACE_ERRBOX (num$(TAB_DATE(I+1))-"pour"-TAB_CHAINE(I)) From ZSYRADOM1
Next I
 
Call TRACE_ERRBOX(num$(TAB_DECIMAL(0))+"."+sigma(1,NOMBRE,num$(TAB_DECIMAL(indcum)))) From ZSYRADOM1
 
ERREUR_TROUVEE=0
Errbox TAB_CHAINE(dim(TAB_CHAINE)+1)
Errbox num$(ERREUR_TROUVEE)
 
Call TRIE_SEULEMENT_CA(TAB_CHAINE(1..4))
Call TRACE_ERRBOX("Tri partiel de 1 à 4") From ZSYRADOM1
For I=0 To find("",TAB_CHAINE)-2+(find("",TAB_CHAINE)=0)*(dim(TAB_CHAINE)+1)
  Call TRACE_ERRBOX (TAB_CHAINE(I)) From ZSYRADOM1
Next I
 
End
 
Subprog TRIE_MOI_CA(ARG1,ARG2,ARG3)
Variable Char ARG1()(2..)
Variable Decimal ARG2(-5..)
Variable Date ARG3(0..)
Sorta find("",ARG1)-1+(find("",ARG1)=0)*dim(ARG1) ARG1,ARG2,ARG3
End
 
Subprog RETRIE_MOI_CA(ARG1,ARG2,ARG3)
Variable Char ARG1()()
Variable Decimal ARG2()
Variable Date ARG3()
Sorta ARG1,ARG2,ARG3 Order By ARG3(indice) Desc
End
 
Subprog TRIE_SEULEMENT_CA(ARG1)
Variable Char ARG1()
Sorta ARG1
End
 
$TROP_LOIN
ERREUR_TROUVEE=errn
Resume
